// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {

    ext {
        kotlin_version = '1.3.31'
        kotlin_serialization_version = '0.11.0'
        coroutines_version = "1.2.0"

        junit_version = "4.12"

        mockk_version = "1.9.3"
        assertk_version = "0.13"
        detekt_version = "1.0.0-RC14"

        moshi_version = "1.8.0"
        okhttp_version = "3.14.1"

        bivrost_version = "v0.7.1"
        kmnid_version = "0.3.2"
        kethereum_version = "0.75.1"
        spongycastle_version = "1.58.0.0"
        uport_kotlin_common_version = "0.1.1"

        current_release_version = "0.2.1"
    }

    repositories {
        google()
        jcenter()
        maven { url "https://jitpack.io" }
        maven { url "https://kotlin.bintray.com/kotlinx" }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-serialization:$kotlin_version"
        classpath "com.github.gnosis.bivrost-kotlin:bivrost-gradle-plugin:$bivrost_version"

        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4"

        classpath "io.gitlab.arturbosch.detekt:detekt-gradle-plugin:$detekt_version"
    }
}

plugins {
    id "io.errorlab.gradle.vault" version "0.1.0"
}

apply plugin: "io.gitlab.arturbosch.detekt"

detekt {
    version = detekt_version
    input = files(
            "$projectDir"
    )
    //config = "${projectDir}/detekt.yml"
    filters = ".*test.*,.*/resources/.*,.*/tmp/.*,.*/build/.*"
    parallel = true
    reports {
        xml {
            enabled = true
            destination = file("${project.buildDir}/reports/detekt.xml")
        }
        html {
            enabled = true
            destination = file("${project.buildDir}/reports/detekt.html")
        }
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        maven { url 'https://jitpack.io' }
        maven { url "https://kotlin.bintray.com/kotlinx" }
//        mavenLocal()
    }

//address warnings about multiple kotlin runtimes in classpath
    configurations.all {
        resolutionStrategy.dependencySubstitution {
            substitute module("org.jetbrains.kotlin:kotlin-stdlib-jre7") because "warning about multiple runtimes in the classpath" with module("org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version")
            substitute module("org.jetbrains.kotlin:kotlin-stdlib-jre8") because "warning about multiple runtimes in the classpath" with module("org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version")

        }
        resolutionStrategy.eachDependency { DependencyResolveDetails details ->
            def requested = details.requested
            if (requested.group == 'org.jetbrains.kotlin' && requested.name in [
                    'kotlin-reflect', 'kotlin-stdlib', 'kotlin-stdlib-common',
                    'kotlin-stdlib-jdk7', 'kotlin-stdlib-jdk8',
            ]) {
                details.useVersion kotlin_version
            }
        }
    }
}

subprojects { subproject ->
    afterEvaluate {
        subproject.tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
            kotlinOptions {
                jvmTarget = "1.8"
            }
        }
    }
}

apply from: "publishing.gradle"

task clean(type: Delete) {
    delete rootProject.buildDir
}